//@version=5
indicator("Contraction Free Candle", overlay=true)

// Input for colors
textColor = input.color(color.white, title="Text Color")
boxColor = input.color(color.new(color.purple, 90), title="Box Color")

// Get the body of the current candle A (index 1)
openA = open[1]
closeA = close[1]

// Get the body of Candle X (left candle) and Candle B (right candle)
openX = open[2]
closeX = close[2]
openB = open
closeB = close

// Determine the body range of Candle A
bodyHighA = math.max(openA, closeA)
bodyLowA = math.min(openA, closeA)

// Identify inside bars
insideBarX = (math.max(openX, closeX) <= bodyHighA and math.min(openX, closeX) >= bodyLowA)
insideBarB = (math.max(openB, closeB) <= bodyHighA and math.min(openB, closeB) >= bodyLowA)
extendedInsideBar = insideBarX and insideBarB

// Track contraction range
var float contractionHigh = na
var float contractionLow = na
var int candleCounter = na 
if extendedInsideBar
    contractionHigh := bodyHighA
    contractionLow := bodyLowA
    candleCounter := 0
else if not na(contractionHigh) and not na(contractionLow)
    candleCounter := nz(candleCounter) + 1

// Identify free candle
var bool FreeDetected = false
freeCandle = not na(contractionHigh) and not na(contractionLow) and ((low > contractionHigh) or (high < contractionLow))

// Mark first free candle with label
if freeCandle and not FreeDetected
    label.new(x=bar_index, y=high, text="C: " + str.tostring(candleCounter) + "\nTF: " + timeframe.period, color=boxColor, style=label.style_label_down, textcolor=textColor)
    FreeDetected := true

// Reset detection for new extended inside bars
if extendedInsideBar
    FreeDetected := false

// Bar colors with titles
barcolor(extendedInsideBar and insideBarX ? color(color.red) : na, offset=-2, title="Contraction Bar A")
barcolor(extendedInsideBar ? color(color.orange) : na, offset=-1, title="Contraction Bar B")
barcolor(extendedInsideBar and insideBarB ? color(color.green) : na, title="Contraction Bar C")
barcolor(freeCandle ? color(color.purple) : na, title="Free Candle")
