//@version=6
indicator('FC v2', overlay = true)

// Input for colors
textColor = input.color(color.white, title = 'Text Color')
boxColor = input.color(color.new(color.purple, 90), title = 'Box Color')

// Get the body of the current candle A (index 1)
openA = open[1]
closeA = close[1]

// Get the body of Candle X (left candle) and Candle B (right candle)
openX = open[2]
closeX = close[2]
openB = open
closeB = close

// Determine the body range of Candle A
bodyHighA = math.max(openA, closeA)
bodyLowA = math.min(openA, closeA)

// Identify inside bars
insideBarX = math.max(openX, closeX) <= bodyHighA and math.min(openX, closeX) >= bodyLowA
insideBarB = math.max(openB, closeB) <= bodyHighA and math.min(openB, closeB) >= bodyLowA
extendedInsideBar = insideBarX and insideBarB

// Track contraction range
var float contractionHigh = na
var float contractionLow = na
var int candleCounter = na
if extendedInsideBar
    contractionHigh := bodyHighA
    contractionLow := bodyLowA
    candleCounter := 0
else if not na(contractionHigh) and not na(contractionLow)
    candleCounter := nz(candleCounter) + 1

// Identify free candle
freeCandle = not na(contractionHigh) and not na(contractionLow) and (low > contractionHigh or high < contractionLow)

// Determine label position based on free candle breakout
aboveRange = low > contractionHigh  // Free candle broke above contraction range
belowRange = high < contractionLow  // Free candle broke below contraction range

labelYPos = aboveRange ? high : low  // Place label above or below accordingly
labelStyle = aboveRange ? label.style_label_down : label.style_label_up  // Flip shape accordingly

// Track first free candle detection
var bool FreeDetected = false
if freeCandle and not FreeDetected
    label.new(x = bar_index, y = labelYPos, text = 'C: ' + str.tostring(candleCounter) + '\nTF: ' + timeframe.period, color = boxColor, style = labelStyle, textcolor = textColor)
    FreeDetected := true

// Reset detection for new extended inside bars
if extendedInsideBar
    FreeDetected := false

// Bar colors with titles
barcolor(extendedInsideBar and insideBarX ? color.red : na, offset = -2, title = 'Contraction Bar A')
barcolor(extendedInsideBar ? color.orange : na, offset = -1, title = 'Contraction Bar B')
barcolor(extendedInsideBar and insideBarB ? color.green : na, title = 'Contraction Bar C')
barcolor(freeCandle ? color.purple : na, title = 'Free Candle')
